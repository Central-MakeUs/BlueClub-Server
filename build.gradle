buildscript {
	ext {
		restdocsApiSpecVersion = '0.17.1'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'

	// RestDocs
	id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"

	// Swagger
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'blueclub'
version = '0.3.0-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

swaggerSources {
	sample {
		setInputFile(file("build/api-spec/index.yaml"))
		code {
			language = 'spring'
		}
	}
}

openapi3 {
	servers = [
			{ url = "https://www.blueclubserver.shop" },
			{ url = "http://localhost:8080" }
	]
	title = "BlueClub Swagger UI"
	description = "BlueClub Spring REST Docs with Swagger UI."
	version = "0.3.0"
	format = "yaml"
	outputFileNamePrefix = 'index'
}

task swaggerConfig {
	dependsOn 'openapi3'
	doFirst {
		def swaggerUIFile = file('build/api-spec/index.yaml')

		def securitySchemesContent =  "  securitySchemes:\n" +  \
                                      "    bearerAuth:\n" +  \
                                      "      type: http\n" +  \
                                      "      scheme: bearer\n" +  \
                                      "      bearerFormat: JWT\n" + \
                                      "security:\n" +
									  "  - bearerAuth: []"

		swaggerUIFile.append(securitySchemesContent)
	}
	finalizedBy 'copyConfig'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	// Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// OAuth & JWT
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-config:6.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	runtimeOnly 'com.auth0:java-jwt:4.2.1'
	implementation 'com.auth0:java-jwt:4.2.1'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.projectlombok:lombok:1.18.26'

	// DB
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'com.h2database:h2'

	// Redis
	implementation 'it.ozimov:embedded-redis:0.7.2'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// AWS S3
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// Rest Docs & Swagger
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:' + restdocsApiSpecVersion
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
	swaggerUI 'org.webjars:swagger-ui:4.18.1'

	// Slack
	implementation 'com.slack.api:slack-api-client:1.29.0'

	// Querydsl
	implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
	implementation "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	compileOnly 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

def querydslSrcDir = 'src/main/generated'
sourceSets {
	main {
		java {
			srcDirs += [ querydslSrcDir ]
		}
	}
}

compileJava {
	options.compilerArgs << '-Aquerydsl.generatedAnnotationClass=javax.annotation.Generated'
}

tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

clean {
	delete file(querydslSrcDir)
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyConfig {
	doLast {
		copy {
			from 'build/api-spec'
			into 'build/resources/main/static/docs'
		}
		copy {
			from 'build/resources/main/static/docs'
			into 'src/main/resources/static/docs'
		}
	}
}

bootJar {
	dependsOn 'swaggerConfig'
}